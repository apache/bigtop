/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * <p/>
 * http://www.apache.org/licenses/LICENSE-2.0
 * <p/>
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def create_collection() {
  exec {
    executable "/usr/bin/solrctl"
    args "instancedir", "--generate", "/tmp/smoke_data"
  }
  exec {
    executable "/usr/bin/solrctl"
    args "instancedir", "--create", "smoke", "/tmp/smoke_data"
  }
  exec {
    executable "/usr/bin/solrctl"
    args "collection", "--create", "smoke"
  }
  exec {
    executable "/usr/bin/solrctl"
    args "collection", "--config", "smoke", '{"set-property": {"requestDispatcher.requestParsers.enableRemoteStreaming": true}, "set-property": {"requestDispatcher.requestParsers.enableStreamBody": true}}'
  }
}

def delete_collection() {
  exec {
    executable "/usr/bin/solrctl"
    args "collection", "--delete", "smoke"
  }
  exec {
    executable "/usr/bin/solrctl"
    args "instancedir", "--delete", "smoke"
  }
}

def clean() {
  if (file("/tmp/smoke_data").exists()) {
    exec {
      executable "/usr/bin/rm"
      args "-rf", "/tmp/smoke_data"
    }
  }
}

def junitVersion = '4.11'
dependencies {
  compile group: 'junit', name: 'junit', version: junitVersion, transitive: 'true'
  compile group: 'org.apache.solr', name: 'solr-solrj', version: solrVersion, transitive: 'true'
  compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion, transitive: 'true'
}

sourceSets {
  test {
    groovy {
      srcDirs = [
        "${BIGTOP_HOME}/bigtop-tests/test-artifacts/solr"
      ]
    }
  }
}

test.doFirst {
  create_collection()
}

test.doLast {
  delete_collection()
  clean()
}

test {
  include '**/Test*'
  systemProperty "org.apache.bigtop.itest.solr_url", "http://localhost:8983/solr/smoke"
}
